# Puuro main file

builtin.pollute builtin;

(args.size < 2).then {
    raise (exception 'not_enough_arguments "give more arguments to the program");
};

builtin.active_frame.set_exception_handler {<e c>
    print "exception: " e.name.s ", " e.obj;
    (e.name == 'system_error).then {
        print "system error: " (((std2.get_module 'libc).get_function 'strerror) e.obj);
    };
    print "trace:";
    repeat {
        print "  " c.current_file.s ", " c.current_line;
        c = c.caller;
        c != \n;
    };
};

puuro_executable = args.first;
script_file      = args.second;

args = args.all_after 2;

execute = {<internal fn>
  f = builtin.compile_file fn;
  f.set_exception_handler builtin.active_frame.get_exception_handler;
  f.scope.pollute builtin;
  f.scope.set_local 'builtin builtin;
  internal.if {
      f.scope.set_local 'resolver resolver;
      f.scope.set_local 'std2 std2;
      f.scope.set_local 'execute execute;
      f.execute;
  }
  {
      f.scope.set_local 'args args;
      f.switch;
  };
};

["exceptions.puuro"
 "module.puuro"
 "list.puuro"
 "utils.puuro"
 "string.puuro"].each {<x>
    execute \t lib_dir + "/" + x
};

['streams 'blocking 'file 'web 'net 'irc 'dir 'std2 'filechange 'interactive].each {<m>
    builtin.module_source m (lib_dir + "/" + m.s + ".puuro");
};

execute \f script_file;
