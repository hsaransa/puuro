# Weird features

std.type.set_method 'multi
{<std m s *r>
    std.print m " " s " " ;
    r.each
    {<l>
        s = std.apply (s.type.get_method m):rcall [s] + r;
    };
    s;
};


std.type.set_method 'bg
{<std code>
    act = \f;

    std.new_continuation {<orig>
        std.active_frame.set_exception_handler orig.get_exception_handler;
        act = std.active_frame;

        std.sleep 0 orig;

        act = \f;
        code.call;
    };

    act;
};

std.type.set_method 'range {<std s e>
  i = s;
  l = [];

  std.while {i <= e}
  {
      l.append i;
      i = i + 1;
  };

  l;
};

std.type.set_method 'min {<std *l>
    v = l.first;
    (l.all_after 1).each {<x>
        (x < v) { v = x };
    };
    v;
};

std.type.set_method 'max {<std *l>
    v = l.first;
    (l.all_after 1).each {<x>
        (x > v) { v = x };
    };
    v;
};

std.type.set_method 'all {<std *l>
    v = \t;
    l.each {<x> v = v.and x};
    v;
};

std.type.set_method 'any {<std *l>
    v = \f;
    l.each {<x> v = v.or x};
    v;
};

std.type.set_method 'assert {<std c *o>
    c.else { raise (exception 'assertion_failed o); }
};

std.type.set_method 'switch {<std value *cases>
    ret = \f;

    repeat
    {
        (cases.len < 2).if
        {
            ret = cases.first();
            cases = [];
        }
        {
            (value == cases.first).if
            {
                ret = cases.second();
                cases = [];
            }
            {
                cases = cases.all_after 2;
            };
        };

        !cases.empty;
    };

    ret;
};

std.type.set_method 'break {<std obj>
    raise (exception 'break obj);
};

std.type.set_method 'block {<std c>
    f = std.active_frame;
    prev = f.get_exception_handler;

    ret = new_continuation {<orig>
        ff = c.call_frame;

        ff.set_exception_handler {<e c>
            (e.name == 'break).then {
                orig.continue e.obj;
            };
            prev e c;
        };

        ff.execute;
        orig.continue \n;
    };

    ret;
};
