# Simple streaming web server.

require 'streams;

start_web_server = {<port client_code>
    f = std.file;
    f.socket 'inet 'stream;
    f.setsockopt 'reuseaddr \t;
    addr = resolver.inaddr_any;
    addr.set_port port;
    f.bind addr;
    f.listen 5;

    repeat {
        c = f.accept.first;
        bg {
            #try {
                handle_client c client_code
            #} {<e c>
            #    print "exception " e.name " " e.obj;
            #};
            #print "keep going.";
        };
    };
};

handle_client = {<s client_code>
    socket_in = streamer {<in>
        repeat {
            a = in();
            (a != \n).then { s.write a; };
        };
        s.close;
    };

    socket_out = streamer {<out>
        repeat {
            out (s.read 1024);
        };
    };

    http = streamer {<in din out dout>
        buf = "";
        repeat
        {
            ss = in();
            (ss == \n).then { raise (exception 'failed 0) };
            buf = buf + ss;
            (buf.find "\r\n") < 0;
        };

        line, buf = buf.split "\r\n" 1;

        print "LINE " line;

        method, path, version = line.split " ";
        ((method != "GET").and method != "POST") { raise (exception 'failed 0) };
        method = method.to_symbol;
        dout method;
        dout path;
        ((version.all_before 6) != "HTTP/1") { raise (exception 'failed 0) };

        headers = [];

        repeat
        {
            while {(buf.find "\r\n") > 0}
            {
                l, buf = buf.split "\r\n" 1;
                headers.append (l.split ": ");
            };
            ((buf.find "\r\n") != 0).then
            {
                ss = in();
                (ss == \n).then { raise (exception 'failed 0) };
                buf = buf + ss;
            }
        };

        dout headers;

        buf = buf.all_after 2;

        (method == 'POST).then {
            cl = headers.filter {<p> p.first == "Content-Length" };
            len = cl.first.second.to_integer;

            while {buf.size < len}
            {
                ss = in();
                buf = buf + ss;
            };

            dout buf;
        };

        repeat
        {
            a = din();
            (a != \n).then { out a };
        };

        out \n;
    };

#-
    lollo = streamer {<in dout>
        path = in();
        headers = in();

        dout "HTTP/1.0 200 OK\r\n";
        dout "Content-type: text/html\r\n";
        dout "\r\n";
        dout "Ei muuta\r\n";
        dout \n;
    };
-#

    client = streamer client_code;

    http.connect socket_in;
    socket_out.connect http;
    http.connect client;
    client.connect http;

    try
    {
        http.start;
    }
    {<e c>
        s.close;
        (std.active_frame.get_exception_handler) e c;
    };
};


module 'web
{
    pollute 'web_server start_web_server;
};
